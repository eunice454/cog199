
    model {
      bound_mean ~ dnorm(1.5, pow(0.4, -2))T(0.10, 5.00)
      nondt_mean ~ dnorm(0.3, pow(0.05, -2))T(0.05, 1.0)
      drift_mean ~ dnorm(0, pow(0.5, -2))T(-5.00, 5.00)
    
      bound_sdev ~ dunif(0.05, 0.5)
      nondt_sdev ~ dunif(0.05, 2)
      drift_sdev ~ dunif(0.05, 2)
    
      # Sampling model
      for (p in 1:n_Participants) {
        bound[p] ~ dnorm(bound_mean, pow(bound_sdev, -2))T(0.10, 5.00)
        nondt[p] ~ dnorm(nondt_mean, pow(nondt_sdev, -2))T(0.05, 1.0)
        drift[p] ~ dnorm(drift_mean, pow(drift_sdev, -2))T(-5.00, 5.00)
    
        # Forward equations from EZ Diffusion
        ey[p] = exp(-bound[p] * drift[p])
        Pc[p] = 1 / (1 + ey[p])
        PRT[p] = 2 * pow(drift[p], 3) / bound[p] * pow(ey[p] + 1, 2) / (2 * -bound[p] * drift[p] * ey[p] - pow(ey[p], 2) + 1)
        MDT[p] = (bound[p] / (2 * drift[p])) * (1 - ey[p]) / (1 + ey[p])
        MRT[p] = MDT[p] + nondt[p]
    
        # Loss functions using MRT, PRT, and Pc
        correct[p] ~ dbin(Pc[p], n_trials)
        meanRT[p] ~ dnorm(MRT[p], PRT[p] * n_trials)
        varRT[p] ~ dnorm(1 / PRT[p], 0.5 * (n_trials - 1) * PRT[p] * PRT[p])
      }
    }

